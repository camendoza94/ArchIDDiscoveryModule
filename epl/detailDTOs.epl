import './queries.eol';
pattern CorrectFields
	class: ClassDeclaration
		guard: class.proxy = false and class.name.endsWith("DetailDTO"),
	field: FieldDeclaration
		from: class.bodyDeclarations -> select (f | (f.type.type.isTypeOf(ClassDeclaration) and not f.type.type.name.endsWith("DTO")) 
		or (f.type.type.isTypeOf(ParameterizedType) and not (f.type.type.type.type.name.endsWith("List") or f.type.type.typeArguments.type.name.endsWith("DTO")))) {
	onmatch {
		var message : String;
 		message = "CorrectFields " + class.name + " field: " + field.getName();
		message.println();
		var comment = new LineComment;
		var rule: rulesModel!Rule;
		rule = rulesModel!ReferenceArchitecture.all.first.findRule(7);
		var issues = new Native("archtoring.tools.IssuesTool");
		issues.addIssue(rule.id, class.getPath(), field.getName());
	}
}

pattern CreateDTOFromEntity
	class: ClassDeclaration
		guard: class.proxy = false and class.name.endsWith("DetailDTO"),
	no constructor: ConstructorDeclaration
		from: class.bodyDeclarations -> select (c | c.isTypeOf(ConstructorDeclaration) and c.proxy = false 
			and c.parameters.size = 1 and c.parameters.at(0).type.type.name.endsWith("Entity") and c.body.statements.size > 1
			and c.body.statements.at(0).isTypeOf(SuperConstructorInvocation)
			and (c.body.statements.select(s |s.isTypeOf(IfStatement)
			and s.expression.isTypeOf(InfixExpression)
			and s.expression.rightOperand.isTypeOf(NullLiteral)
			and s.expression.operator = InfixExpressionKind#NOT_EQUALS).size) > 0) {
	onmatch {
		var message : String;
 		message = "CreateDTOFromEntity " + class.name; 
		message.println();
		var rule: rulesModel!Rule;
		rule = rulesModel!ReferenceArchitecture.all.first.findRule(8);
		var issues = new Native("archtoring.tools.IssuesTool");
		issues.addIssue(rule.id, class.getPath(), "Class");
	}
}

pattern toEntity
	class: ClassDeclaration
		guard: class.proxy = false and class.name.endsWith("DetailDTO"),
	no method: MethodDeclaration
		from:  class.bodyDeclarations -> select (m | m.name = "toEntity" and m.parameters.size = 0 and m.body.statements.size > 1 and m.body.statements.at(0).isTypeOf(VariableDeclarationStatement)
			and m.body.statements.at(0).fragments.at(0).initializer.method.name ="toEntity") {
	onmatch {
	var message : String;
 		message = "toEntity " + class.name;
		message.println();
		var rule: rulesModel!Rule;
		rule = rulesModel!ReferenceArchitecture.all.first.findRule(9);
		var issues = new Native("archtoring.tools.IssuesTool");
		issues.addIssue(rule.id, class.getPath(), "Class");
	}
}

@cached
operation FieldDeclaration getName() {
	return self.fragments.at(0).name.firstToUpperCase();
}